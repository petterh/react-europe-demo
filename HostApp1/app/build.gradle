apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.contoso.hostapp1"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // React Native doesn't currently support 64-bit binaries
        // https://corbt.com/posts/2015/09/18/mixing-32-and-64bit-dependencies-in-android.html
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// https://docs.gradle.org/current/userguide/working_with_files.html#sec:copying_files
// https://docs.gradle.org/3.4/javadoc/org/gradle/api/tasks/Copy.html
task copyAssets(type: Copy) {
    from "$rootDir/../Artifacts/assets"
    into "src/main/assets"
}

// http://stackoverflow.com/questions/16853130/run-task-before-compilation-using-android-gradle-plugin
afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.javaCompiler.dependsOn(copyAssets)
    }
}

dependencies {
    copyAssets
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.contoso.react:reactnativedemolibrary:0.2.0'

    testCompile 'junit:junit:4.12'
}
