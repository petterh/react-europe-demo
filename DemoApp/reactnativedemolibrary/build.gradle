apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Android binaries) is installed from npm.
            // This is the approach generated by react-native init, and the one described here:
            // https://facebook.github.io/react-native/docs/integration-with-existing-apps.html
            // The module 'reactnativedemolibrary' uses this approach.
            url "$rootDir/reactnativedemolibrary/node_modules/react-native/android"
        }
    }
}

// http://stackoverflow.com/questions/34872382/manually-adding-aar-with-dependency-pom-iml-file
publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.contoso.reactnativedemolibrary'
            artifactId 'reactnativedemolibrary'
            version '0.1'
            artifact "$buildDir/outputs/aar/reactnativedemolibrary-release.aar" //aar artifact you want to publish

            // Generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    if (dependency.group) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }

    // Publish to filesystem repo
    repositories{
        maven {
            url "$rootDir/../Artifacts/Maven"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile "com.facebook.react:react-native:0.44.0"

    testCompile 'junit:junit:4.12'
}
